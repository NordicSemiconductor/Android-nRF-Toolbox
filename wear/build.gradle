apply plugin: 'com.android.application'

android {

    defaultConfig {
        applicationId "no.nordicsemi.android.nrftoolbox"
        minSdkVersion 23
        targetSdkVersion 26
        compileSdkVersion 26
        buildToolsVersion "26.0.2"
        versionCode 202225700 // target: 25, version: 2.2.2, build: 57, multi-APK: 01
        versionName "2.2.2"
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {

    }
}

// exclude these from the build:
configurations.all() { configuration ->
    exclude group: "org.apache.httpcomponents", module: "httpclient"
}

dependencies {

    implementation project(':common')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'no.nordicsemi.android.support.v18:scanner:1.0.0'
    compileOnly 'com.google.android.wearable:wearable:2.1.0'
    implementation('com.google.android.support:wearable:2.1.0') {}

    // https://developer.android.com/training/wearables/ui/wear-ui-library.html
    // implementation 'com.android.support:wear:27.0.0'

    // nRF Toolbox is using Play Service 10.2.0 in order to make the app working in China:
    // https://developer.android.com/training/wearables/apps/creating-app-china.html#ChinaSDK
    //noinspection GradleDependency
    implementation 'com.google.android.gms:play-services-wearable:10.2.0'
}

// temporary workaround for support library version mismatch
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (! requested.name.startsWith("multidex")) {
                details.useVersion '26.0.2'
            }
        }
    }
}
